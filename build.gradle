plugins {
	id 'com.diffplug.blowdryer'
	id 'com.diffplug.spotless-changelog'
}

spotlessChangelog {
	forceNextVersion '1.0.0'
}
apply from: 干.file('changelog.gradle')

apply plugin: 'java'
apply plugin: 'com.adarshr.test-logger'
sourceCompatibility = java_compat
targetCompatibility = java_compat
tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

apply from: 干.file('spotless.gradle')

jar.manifest.attributes(
		'Bundle-SymbolicName':    'dev.equo.ide.chatgpt; singleton:=true',
		'Bundle-IdeHook':         'dev.equo.ide.chatgpt.ChatGptIdeHook',
		'Bundle-Version':         spotlessChangelog.versionNext,
		'Implementation-Version': spotlessChangelog.versionNext)

// compile and test against the eclipse platform
apply plugin: 'dev.equo.p2deps'
p2deps {
	into ([
		'compileOnly',
		'testImplementation'
	], {
		platform()
	})
}

repositories {
	maven {
		url 'https://dl.equo.dev/chromium-swt-ee/equo-gpl/mvn'
	}
}

import com.diffplug.common.swt.os.SwtPlatform

String VER_DURIAN = '1.2.0'
dependencies {
	compileOnly "com.equo:com.equo.chromium:$VER_EQUO_CHROMIUM"
	testImplementation "com.equo:com.equo.chromium:$VER_EQUO_CHROMIUM"
	testImplementation "com.equo:com.equo.chromium.cef.${SwtPlatform.getRunning()}:$VER_EQUO_CHROMIUM"
	implementation "dev.equo.ide:solstice:$VER_SOLSTICE"
	implementation "com.diffplug.durian:durian-swt:$VER_DURIAN_SWT"
	implementation "com.diffplug.durian:durian-rx:4.0.1"
	implementation "com.diffplug.durian:durian-core:$VER_DURIAN"
	implementation "com.diffplug.durian:durian-collect:$VER_DURIAN"
	implementation "com.diffplug.durian:durian-concurrent:$VER_DURIAN"
	testImplementation "org.junit.jupiter:junit-jupiter:$VER_JUNIT"
	testImplementation "org.assertj:assertj-core:$VER_ASSERTJ"
}

apply from: 干.file('maven.gradle')
apply from: 干.file('sonatype.gradle')
test {
	useJUnitPlatform()
	if (com.diffplug.common.swt.os.OS.getNative().isMac()) {
		jvmArgs = ['-XstartOnFirstThread']
	}
	if (System.getenv('CI') != null) {
		systemProperty 'com.diffplug.test.autoclose.milliseconds', '500'
	}
}

apply plugin: 'dev.equo.ide'
equoIde {
	gradleBuildship().autoImport('.')
	equoChromium()
	dogfood()
}